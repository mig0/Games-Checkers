#!/usr/bin/perl

use strict;
use warnings;

my $size_x = shift || die "Pass size_x\n";
my $size_y = shift || die "Pass size_y\n";

my $size_x_2 = $size_x / 2;

my $locs = $size_x * $size_y / 2;
my $size = $size_x > $size_y ? $size_y : $size_x;
my $default_rows = $size_y / 2 - 1;

sub loc_to_arr ($) {
	my $loc = shift;
	return (int($loc % $size_x_2) * 2 + int(($loc / $size_x_2) % 2) + 1, int($loc / $size_x_2) + 1);
}

sub arr_to_loc ($$) {
	my ($x, $y) = @_;
	return 'NL' if $x < 1 || $x > $size_x || $y < 1 || $y > $size_y;
	die if ($x + $y) % 2;
	return sprintf "%2d", int(($x - 1) / 2) + ($y - 1) * $size_x_2;
}

sub strip_nl ($) {
	my $str = shift;
	$str =~ s/NL/  /g;
	$str =~ s/ ,/  /g;
	$str =~ s/,( +])/ $1/g;
	return $str;
}

sub format_items ($;$$) {
	my $items = shift || die;
	my $prefix = shift || '';
	my $inline = shift || 0;

	my $str = strip_nl("$prefix\[ " . join(', ',
		map { ref($_) eq 'ARRAY' ? '[ ' . join(', ', @$_) . ' ]' : $_ } @$items
	) . " ]");
	$str = "\t$str,\n" unless $inline;

	return $str;
}

sub format_bunches ($;$) {
	my $items = shift || die;
	my $leave_nl = shift || 0;

	die "Internal: Number of items for bunches is less than $size_x_2\n"
		if @$items < $size_x_2;

	my $str = "\t" . join(', ',
		map { ref($_) eq 'ARRAY' ? '[ ' . join(', ', @$_) . ' ]' : $_ } @$items[0 .. $size_x_2 - 1]
	) . ",\n";
	$str = strip_nl($str) unless $leave_nl;

	$str .=
		&format_bunches([ @$items[$size_x_2 .. @$items - 1] ], $leave_nl)
		if @$items > $size_x_2;

	return $str;
}

my @loc_direction_locs;
my @pawn_step_locs = ([], []);
my @pawn_beat_locs;
my @pawn_beat_forward_locs = ([], []);
my @pawn_beat_8dirs_locs;
my @king_step_short_locs;
my @king_beat_short_locs;
my $king_step_str;
my $king_beat_str;
my $king_beat_8dirs_str;
my $enclosed_locs_str;
my $enclosed_locs_8dirs_str;

for (0 .. $locs - 1) {
	my ($x, $y) = loc_to_arr($_);
	my @king_step_locs;
	my @king_beat_locs;
	my @king_beat_8dirs_locs;
	my %enclosed_locs;
	my %enclosed_8dirs_locs;
	my @enclosed_locs1 = ();
	my @enclosed_locs2 = ();
	my @enclosed_locs3 = ();
	my @enclosed_locs4 = ();
	my @enclosed_locs5 = ();
	my @enclosed_locs6 = ();
	my @enclosed_locs7 = ();
	my @enclosed_locs8 = ();
	for my $i (1 .. $size - 1) {
		my $l1 = arr_to_loc($x - $i, $y + $i);
		my $l2 = arr_to_loc($x + $i, $y + $i);
		my $l3 = arr_to_loc($x - $i, $y - $i);
		my $l4 = arr_to_loc($x + $i, $y - $i);
		my $l5 = arr_to_loc($x,  $y - 2 * $i);
		my $l6 = arr_to_loc($x - 2 * $i,  $y);
		my $l7 = arr_to_loc($x,  $y + 2 * $i);
		my $l8 = arr_to_loc($x + 2 * $i,  $y);
		push @loc_direction_locs, [ $l1, $l2, $l3, $l4 ] if $i == 1;
		push @{$pawn_step_locs[0]}, [ $l1, $l2 ] if $i == 1;
		push @{$pawn_step_locs[1]}, [ $l3, $l4 ] if $i == 1;
		push @pawn_beat_locs, [ $l1, $l2, $l3, $l4 ] if $i == 2;
		push @{$pawn_beat_forward_locs[0]}, [ $l1, $l2 ] if $i == 2;
		push @{$pawn_beat_forward_locs[1]}, [ $l3, $l4 ] if $i == 2;
		push @pawn_beat_8dirs_locs, [ $l1, $l2, $l3, $l4, $l5, $l6, $l7, $l8 ] if $i == 2;
		push @king_step_short_locs, [ $l1, $l2, $l3, $l4 ] if $i == 1;
		push @king_beat_short_locs, [ $l1, $l2, $l3, $l4 ] if $i == 2;
		push @king_step_locs, $l1, $l2, $l3, $l4;
		push @king_beat_locs, $l1, $l2, $l3, $l4 if $i > 1;
		push @king_beat_8dirs_locs, $l1, $l2, $l3, $l4, $l5, $l6, $l7, $l8 if $i > 1;
		@enclosed_locs1 = () if $l1 eq 'NL';
		@enclosed_locs2 = () if $l2 eq 'NL';
		@enclosed_locs3 = () if $l3 eq 'NL';
		@enclosed_locs4 = () if $l4 eq 'NL';
		@enclosed_locs5 = () if $l5 eq 'NL';
		@enclosed_locs6 = () if $l6 eq 'NL';
		@enclosed_locs7 = () if $l7 eq 'NL';
		@enclosed_locs8 = () if $l8 eq 'NL';
		$enclosed_locs{$l1} = [ @enclosed_locs1 ] if @enclosed_locs1;
		$enclosed_locs{$l2} = [ @enclosed_locs2 ] if @enclosed_locs2;
		$enclosed_locs{$l3} = [ @enclosed_locs3 ] if @enclosed_locs3;
		$enclosed_locs{$l4} = [ @enclosed_locs4 ] if @enclosed_locs4;
		$enclosed_8dirs_locs{$l1} = [ @enclosed_locs1 ] if @enclosed_locs1;
		$enclosed_8dirs_locs{$l2} = [ @enclosed_locs2 ] if @enclosed_locs2;
		$enclosed_8dirs_locs{$l3} = [ @enclosed_locs3 ] if @enclosed_locs3;
		$enclosed_8dirs_locs{$l4} = [ @enclosed_locs4 ] if @enclosed_locs4;
		$enclosed_8dirs_locs{$l5} = [ @enclosed_locs5 ] if @enclosed_locs5;
		$enclosed_8dirs_locs{$l6} = [ @enclosed_locs6 ] if @enclosed_locs6;
		$enclosed_8dirs_locs{$l7} = [ @enclosed_locs7 ] if @enclosed_locs7;
		$enclosed_8dirs_locs{$l8} = [ @enclosed_locs8 ] if @enclosed_locs8;
		push @enclosed_locs1, $l1 if $l1 ne 'NL';
		push @enclosed_locs2, $l2 if $l2 ne 'NL';
		push @enclosed_locs3, $l3 if $l3 ne 'NL';
		push @enclosed_locs4, $l4 if $l4 ne 'NL';
		push @enclosed_locs5, $l5 if $l5 ne 'NL';
		push @enclosed_locs6, $l6 if $l6 ne 'NL';
		push @enclosed_locs7, $l7 if $l7 ne 'NL';
		push @enclosed_locs8, $l8 if $l8 ne 'NL';
	}
	$king_step_str .= format_items(\@king_step_locs);
	$king_beat_str .= format_items(\@king_beat_locs);
	$king_beat_8dirs_str .= format_items(\@king_beat_8dirs_locs);
	$enclosed_locs_str .= "\t{ " . join(', ', map {
		format_items($enclosed_locs{$_}, "$_ => ", 1)
	} sort { $a <=> $b } keys %enclosed_locs) . " },\n";
	$enclosed_locs_8dirs_str .= "\t{ " . join(', ', map {
		format_items($enclosed_8dirs_locs{$_}, "$_ => ", 1)
	} sort { $a <=> $b } keys %enclosed_8dirs_locs) . " },\n";
}

my $loc_directions_str =
	format_bunches(\@loc_direction_locs, 1);
my @is_crowning_strs = (
	format_bunches([ (0) x ($locs - $size_x_2), (1) x $size_x_2 ]),
	format_bunches([ (1) x $size_x_2, (0) x ($locs - $size_x_2) ]),
);
my @pawn_step_strs = (
	format_bunches($pawn_step_locs[0]),
	format_bunches($pawn_step_locs[1]),
);
my $pawn_beat_str =
	format_bunches(\@pawn_beat_locs);
my @pawn_beat_forward_strs = (
	format_bunches($pawn_beat_forward_locs[0]),
	format_bunches($pawn_beat_forward_locs[1]),
);
my $pawn_beat_8dirs_str =
	format_bunches(\@pawn_beat_8dirs_locs);
my $king_step_short_str =
	format_bunches(\@king_step_short_locs);
my $king_beat_short_str =
	format_bunches(\@king_beat_short_locs);

print <<ENDS;
# Games::Checkers, Copyright (C) 1996-2012 Mikhael Goikhman, migo\@cpan.org
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

package Games::Checkers::Board::_${size_x}x$size_y;

use strict;
use warnings;

use base 'Games::Checkers::Board';
use Games::Checkers::Constants;

use constant size_x => $size_x;
use constant size_y => $size_y;
use constant locs => $locs;
use constant default_rows => $default_rows;

use constant loc_directions => [
$loc_directions_str];

use constant is_crowning => [
[
$is_crowning_strs[0]], [
$is_crowning_strs[1]]
];

use constant pawn_step => [
[
$pawn_step_strs[0]], [
$pawn_step_strs[1]]
];

use constant pawn_beat => [
$pawn_beat_str];

use constant pawn_beat_forward => [
[
$pawn_beat_forward_strs[0]], [
$pawn_beat_forward_strs[1]]
];

use constant pawn_beat_8dirs => [
$pawn_beat_8dirs_str];

use constant king_step => [
$king_step_str];

use constant king_beat => [
$king_beat_str];

use constant king_step_short => [
$king_step_short_str];

use constant king_beat_short => [
$king_beat_short_str];

use constant king_beat_8dirs => [
$king_beat_8dirs_str];

use constant enclosed_locs => [
$enclosed_locs_str];

use constant enclosed_8dirs_locs => [
$enclosed_locs_8dirs_str];

1;
ENDS
